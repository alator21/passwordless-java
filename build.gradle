plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}
group = 'io.github.alator21'
version = '1.0.2'
repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}
java {
    withJavadocJar()
    withSourcesJar()
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.alator21'
            artifactId = 'passwordless-java'
            version = '1.0.2'
            from components.java

            pom {
                name = 'Passwordless Java SDK'
                description = 'Passwordless SDK in Java'
                url = 'https://github.com/alator21/passwordless-java'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'alator21'
                        name = 'Rafael Triantafillidis'
                        email = 'botazz133@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/alator21/passwordless-java.git'
                    developerConnection = 'scm:git:ssh://github.com/alator21/passwordless-java.git'
                    url = 'https://github.com/alator21/passwordless-java'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["nexusUsername"]
                password = project.properties["nexusPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
